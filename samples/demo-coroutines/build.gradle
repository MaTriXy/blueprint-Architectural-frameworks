plugins {
    id 'blueprint-plugin'
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    buildFeatures {
        resValues = true
    }
    
    defaultConfig {
        applicationId 'reactivecircus.blueprint.demo.coroutines'
        versionCode 1
        versionName "1.0"
        testApplicationId 'reactivecircus.blueprint.demo.coroutines.test'
        testInstrumentationRunner "reactivecircus.blueprint.demo.CoroutinesScreenTestRunner"
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles('shrinker-rules.pro')
        }
    }
}

androidComponents {
    onVariants(selector().withBuildType("release")) {
        it.addResValue("leak_canary_display_activity_label", "string", "Blueprint Coroutines Demo Leaks", null)
    }
}

dependencies {
    implementation project(':blueprint-interactor-coroutines')
    implementation project(':blueprint-async-coroutines')
    implementation project(':blueprint-ui')
    implementation project(':demo-common')

    // AndroidX
    implementation "androidx.annotation:annotation:${versions.androidx.annotation}"
    implementation "androidx.appcompat:appcompat:${versions.androidx.appCompat}"
    implementation "androidx.coordinatorlayout:coordinatorlayout:${versions.androidx.coordinatorLayout}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintLayout}"
    implementation "androidx.activity:activity-ktx:${versions.androidx.activity}"
    implementation "androidx.fragment:fragment-ktx:${versions.androidx.fragment}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx.recyclerView}"
    implementation "androidx.core:core-ktx:${versions.androidx.core}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidx.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.androidx.lifecycle}"

    // Material Components
    implementation "com.google.android.material:material:${versions.material}"

    // timber
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinx.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinx.coroutines}"

    // Enable LeakCanary for debug builds
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"
    // Fix SDK leaks
    implementation "com.squareup.leakcanary:plumber-android:${versions.leakCanary}"

    // Unit tests
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "io.mockk:mockk:${versions.mockk}"
    testImplementation "org.amshove.kluent:kluent-android:${versions.kluent}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlinx.coroutines}"

    // Android tests
    androidTestImplementation project(':blueprint-testing-robot')
    androidTestImplementation project(':demo-testing-common')
    debugImplementation("androidx.fragment:fragment-testing:${versions.androidx.fragment}") {
        exclude group: 'androidx.test'
    }
    androidTestImplementation "androidx.test:core-ktx:${versions.androidx.test.core}"
    androidTestImplementation "androidx.test:monitor:${versions.androidx.test.monitor}"
    androidTestImplementation "androidx.test:runner:${versions.androidx.test.runner}"
    androidTestImplementation "androidx.test:rules:${versions.androidx.test.rules}"
    androidTestImplementation "androidx.test.ext:junit-ktx:${versions.androidx.test.ext.junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.androidx.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${versions.androidx.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${versions.androidx.espresso}"
}
